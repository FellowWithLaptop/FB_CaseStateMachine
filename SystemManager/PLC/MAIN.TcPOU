<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1">
  <POU Name="MAIN" Id="{b4d7aa96-fa47-4745-a17d-05cd431d0271}" SpecialFunc="None">
    <Declaration><![CDATA[PROGRAM MAIN
VAR
    // state machine
    eState : E_MainStateMachine;
    (*The FB_Init method is used to pass the name of the state machine, the event entry and the variable of the state machine*)
    fbState : FB_CaseStateMachine('MainStateMachine', TC_Events.TcGeneralAdsEventClass.Pending, eState);

    //some transitions
    bInitDone : BOOL;
    bDoStuff : BOOL;
    timertrans1 : TON;
    nCount : INT;
    i : INT;

    //Error
    bError : BOOL;  
END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[(*the case statement is in a repeat loop. If the state is to be changed in the same cycle,
the repeat loop executes the case statement a second time. Be careful when using this function*)
REPEAT

    (*Instead of using an initiger or enum with the Case statement,
      the CyclicCase statement is executed. This is used to pass the step name.*)
    CASE fbState.CyclicCase(TO_STRING(eState)) OF
        E_MainStateMachine.Init:
            
            (*As with a traditional state machine, the state variable can be written to directly*)
            IF bInitDone THEN
                eState := E_MainStateMachine.step0;
                bInitDone := FALSE;
            END_IF
            IF bDoStuff THEN
                eState := E_MainStateMachine.step4;
                bDoStuff := FALSE;
            END_IF
    
        E_MainStateMachine.step0:
            (*Many operations do not have to be performed cyclically.
              The EntryStep and Exit Step properties can be read for this purpose.*)
            IF fbState.EntryStep THEN
                timertrans1.IN := TRUE;
                timertrans1.PT := T#5S;
            END_IF
    
            timertrans1();
            (*Instead of writing an If statement and writing directly to the state variable,
              the TransChangeStep method can be used. When the transition becomes true, the state is changed.*)
              
            fbState.TransChangeState(E_MainStateMachine.step1, timertrans1.Q);
            
            fbState.TransChangeState(E_MainStateMachine.error, bError);
    
            IF fbState.ExitStep THEN
                timertrans1(IN := FALSE);
            END_IF
    
        E_MainStateMachine.step1:
   
            IF fbState.EntryStep THEN //entry step
               nCount := 0;
            END_IF
    
            //do Stuff
            FOR i := 0 TO 10 DO
                nCount := nCount + 1;
            END_FOR
            
            (*Instead of writing an If statement and writing the state variable directly,
            the ChangeStep method can be used to exit the state directly*)
            fbState.ChangeState(E_MainStateMachine.step2);
    
            IF fbState.ExitStep THEN //exit step
                nCount := 0;
            END_IF 
    
        E_MainStateMachine.step2:
        
            (*Some steps are just for organization or preparation.
              You can leave these steps and switch to another one in the same cycle.
              For this purpose you can use the mehode TransChangeStateAndRepeat.*)
            fbState.TransChangeStateAndRepeat(E_MainStateMachine.step3, TRUE);
           
        E_MainStateMachine.step3:
               
            (*do things and repeat the case statement in the same cycle*)
            IF bDoStuff THEN
                fbState.ChangeStateAndRepeat(E_MainStateMachine.step4);
                bDoStuff := FALSE;
            END_IF
            (*Some steps must be executed an exact time.
              Instead of creating TON function blocks you can use the property t.*)
            fbState.TransChangeState(E_MainStateMachine.Init, fbState.t >= T#5S);
            
        E_MainStateMachine.step4:
        
            (*get things done and return to the previous step*)
            fbState.TransChangeState(fbState.PreviousStep, fbState.t >= T#3S);    
        
        E_MainStateMachine.error:
    
            fbState.TransChangeState(E_MainStateMachine.Init, NOT bError);
    END_CASE
    
 (*This checks whether the case statement needs to be executed again.
   Unfortunately there is an error in the compiler which makes it necessary to add an "And True".
   Otherwise no Brakepoints are possible.*)   
UNTIL fbState.NotToBeRepeated AND TRUE END_REPEAT]]></ST>
    </Implementation>
    <LineIds Name="MAIN">
      <LineId Id="190" Count="1" />
      <LineId Id="113" Count="0" />
      <LineId Id="192" Count="0" />
      <LineId Id="114" Count="0" />
      <LineId Id="193" Count="0" />
      <LineId Id="115" Count="2" />
      <LineId Id="194" Count="0" />
      <LineId Id="118" Count="10" />
      <LineId Id="195" Count="0" />
      <LineId Id="129" Count="6" />
      <LineId Id="201" Count="0" />
      <LineId Id="197" Count="0" />
      <LineId Id="136" Count="0" />
      <LineId Id="213" Count="0" />
      <LineId Id="138" Count="6" />
      <LineId Id="146" Count="5" />
      <LineId Id="154" Count="2" />
      <LineId Id="200" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="199" Count="0" />
      <LineId Id="158" Count="6" />
      <LineId Id="212" Count="0" />
      <LineId Id="165" Count="0" />
      <LineId Id="202" Count="1" />
      <LineId Id="166" Count="1" />
      <LineId Id="169" Count="1" />
      <LineId Id="172" Count="4" />
      <LineId Id="204" Count="1" />
      <LineId Id="178" Count="2" />
      <LineId Id="211" Count="0" />
      <LineId Id="181" Count="0" />
      <LineId Id="184" Count="5" />
      <LineId Id="210" Count="0" />
      <LineId Id="207" Count="2" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>